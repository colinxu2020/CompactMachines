buildscript{
    dependencies {
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id "idea"
    id "eclipse"
    id "maven-publish"
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

var envVersion = System.getenv("VERSION") ?: "9.9.9"
if (envVersion.startsWith("v"))
    envVersion = envVersion.trimStart('v')

var RUNNING_IN_CI = true // System.getenv().containsKey("CI") ?: false

archivesBaseName = mod_id
group = "dev.compactmods.compactmachines"
version = envVersion

tasks.register("fgInitCM") {
    doLast {
        println("ForgeGradle+CM initialized")
    }
}

tasks.register("mcVersion") {
    doFirst {
        println("version=$minecraft_version")
    }
}

tasks.register("forgeVersion") {
    doFirst {
        println("version=$forge_version")
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
sourceSets {
    api {
        resources.srcDirs = []
        compileClasspath += configurations.getByName("minecraft")
    }

    datagen {
        compileClasspath += configurations.getByName("minecraft")
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

    tunnels {
        compileClasspath += configurations.getByName("minecraft")
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }

    main {
        java {
            srcDir "src/main/java"
        }

        resources {
            srcDir "src/main/resources"
            srcDir "src/generated/resources"
        }

        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += sourceSets.tunnels.output
        runtimeClasspath += sourceSets.tunnels.output
    }

    test {
        java { srcDir "src/test/java" }
        resources { srcDir "src/test/resources" }

        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

minecraft {
    mappings channel: 'parchment', version: parchment_version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', mod_id
            args '--existing', file('src/main/resources')
            args '--all'
            args '--output', file('src/generated/resources/')

            if(!System.getenv().containsKey("CI")) {
                // JetBrains Runtime Hotswap
                jvmArg '-XX:+AllowEnhancedClassRedefinition'
                jvmArg '-XX:HotswapAgent=fatjar'
            }

            mods {
                compactmachines {
                    source sourceSets.tunnels
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.datagen
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run/gametest')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', ''

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'info'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            if(!System.getenv().containsKey("CI")) {
                // JetBrains Runtime Hotswap
                jvmArg '-XX:+AllowEnhancedClassRedefinition'
                jvmArg '-XX:HotswapAgent=fatjar'
            }
            
            mods {
                compactmachines {
                    source sourceSets.tunnels
                    source sourceSets.api
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
    }
}

if(!RUNNING_IN_CI) {
    minecraft.runs {
        client {
            workingDirectory project.file('run/client')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            if(!RUNNING_IN_CI) {
                // JetBrains Runtime Hotswap
                jvmArg '-XX:+AllowEnhancedClassRedefinition'
                jvmArg '-XX:HotswapAgent=fatjar'
            }

            args '--username', 'Nano'
            args '--width', 1920
            args '--height', 1080

            mods {
                compactmachines {
                    source sourceSets.tunnels
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', mod_id

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            if(!RUNNING_IN_CI) {
                // JetBrains Runtime Hotswap
                jvmArg '-XX:+AllowEnhancedClassRedefinition'
                jvmArg '-XX:HotswapAgent=fatjar'
            }

            mods {
                compactmachines {
                    source sourceSets.tunnels
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.test
                }
            }
        }
    }
}

repositories {
    mavenLocal()

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    // location of the maven that hosts JEI files
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }

    // TheOneProbe
    maven {
        url "https://maven.k-4u.nl"
    }

    maven {
        name "tterrag maven"
        url "https://maven.tterrag.com/"
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-forge:${jei_version}")

    // The One Probe
    implementation(fg.deobf("curse.maven:theoneprobe-245211:4629624"))

    if (!System.getenv().containsKey("CI") && include_test_mods.equals("true")) {
        // Nicephore - Screenshots and Stuff
        //runtimeOnly(fg.deobf("curse.maven:nicephore-401014:3879841"))

        // Testing Mods - Trash Cans, Pipez, Create, Refined Pipes, Pretty Pipes, Refined Storage
        runtimeOnly(fg.deobf("curse.maven:SuperMartijn642Core-454372:5668859"))
        runtimeOnly(fg.deobf("curse.maven:SuperMartijn642Config-438332:4715408"))
        runtimeOnly(fg.deobf("curse.maven:trashcans-394535:4665996"))

        // runtimeOnly(fg.deobf("curse.maven:flywheel-486392:3871082"))
        // runtimeOnly(fg.deobf("curse.maven:create-328085:3737418"))

        // runtimeOnly(fg.deobf("curse.maven:refinedpipes-370696:3570151"))
        // runtimeOnly(fg.deobf("curse.maven:prettypipes-376737:3573145"))
        // runtimeOnly(fg.deobf("curse.maven:refinedstorage-243076:3623324"))

        // Scalable Cat's Force, BdLib, Advanced Generators
        // runtimeOnly(fg.deobf("curse.maven:scalable-320926:3634756"))
        // runtimeOnly(fg.deobf("curse.maven:bdlib-70496:3663149"))
        // runtimeOnly(fg.deobf("curse.maven:advgen-223622:3665335"))

        // Immersive Eng - 7.1.0-145 (Dec 31)
        // runtimeOnly(fg.deobf("curse.maven:immersiveeng-231951:3587149"))

        // FTB Chunks
        // runtimeOnly(fg.deobf("curse.maven:architectury-forge-419699:3781711"))
        // runtimeOnly(fg.deobf("curse.maven:ftb-teams-404468:3725501"))
        // runtimeOnly(fg.deobf("curse.maven:ftblib-404465:3725485"))
        // runtimeOnly(fg.deobf("curse.maven:ftbchunks-314906:3780113"))

        // Mekanism + Mek Generators - Tunnel testing
        runtimeOnly(fg.deobf("curse.maven:mekanism-268560:5662583"))
        runtimeOnly(fg.deobf("curse.maven:mekanismgenerators-268566:5662587"))

        // Soul Shards (FTB)
        // runtimeOnly(fg.deobf("curse.maven:polylib-576589:3751528"))
        // runtimeOnly(fg.deobf("curse.maven:soulshards-551523:3757202"))

        // Everlasting Abilities
        // runtimeOnly(fg.deobf("curse.maven:cyclopscore-232758:3809427"))
        // runtimeOnly(fg.deobf("curse.maven:everlastabilities-248353:3768481"))
    }
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

processTestResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

compileDatagenJava {
    options.encoding = 'UTF-8'
}

jar {
    from sourceSets.tunnels.output
    from sourceSets.main.output
    from sourceSets.api.output
    destinationDirectory = file("$rootDir/build-out")

    finalizedBy('reobfJar')

    manifest {
        attributes([
                "Specification-Title"     : "Compact Machines",
                "Specification-Vendor"    : "",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : "",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    // Sources included because of MinecraftForge/ForgeGradle#369
    archiveClassifier = 'api'
    destinationDirectory = file("$rootDir/build-out")
}

artifacts {
    archives jar, apiJar
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = "$mod_id-forge"
            groupId = "dev.compactmods.compactmachines"

            artifacts {
                artifact jar
                artifact(apiJar) {
                    classifier = "api"
                }
            }
        }
    }

    repositories {
        // GitHub Packages
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/CompactMods/CompactMachines"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
